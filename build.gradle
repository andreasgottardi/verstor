
plugins {
    id 'java-library'
    id 'eclipse'
	id 'maven-publish'
}

sourceCompatibility = 1.8
targetCompatibility = 1.8
group = 'at.goasystems'
version = '0.0.1'

jar {

    manifest {
        attributes 'Implementation-Title': 'Gradle Quickstart', 'Implementation-Version': archiveVersion
    } 
    /*
    from {
        configurations.compileClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    */
}

dependencies {

    /* Logback */
    implementation 'org.slf4j:slf4j-api:1.7.30'
    implementation 'ch.qos.logback:logback-core:1.2.3'
    implementation 'ch.qos.logback:logback-classic:1.2.3'
    
    /* JUnit */
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.0'
    
    /* JGit */
    implementation 'org.eclipse.jgit:org.eclipse.jgit:5.6.0.201912101111-r'
    
    /* https://github.com/apache/commons-io */
	implementation 'commons-io:commons-io:2.6'
	
	/* https://github.com/google/gson */
	implementation 'com.google.code.gson:gson:2.8.6'
}

test {
	useJUnitPlatform()
	systemProperty 'keepTestRepos', keepTestRepos
}

repositories {

    jcenter()
}

sourceSets {
    main {
        resources {
            srcDirs = ["src/main/java", "src/main/resources"]
        }
    }
    test {
        resources {
            srcDirs = ["src/test/java", "src/test/resources"]
        }
    }
}

/* Maven publish logic enabling Gradle to build jar files for sources, javadoc and test code. */

task sourcesJar(type: Jar, dependsOn: classes, group: "build", description: "Generate sources jar") {
	classifier = 'sources'
	duplicatesStrategy=DuplicatesStrategy.EXCLUDE
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc, group: "build", description: "Generate Javadoc jar") {
	classifier = 'javadoc'
	duplicatesStrategy=DuplicatesStrategy.EXCLUDE
	from javadoc.destinationDir
}

task testJar(type: Jar, dependsOn: javadoc, group: "build", description: "Generate tests jar") {
	classifier = 'tests'
	duplicatesStrategy=DuplicatesStrategy.EXCLUDE
	from sourceSets.test.output
}

publishing {
	publications {
		verstor(MavenPublication) {
			from components.java
			artifact sourcesJar
			artifact javadocJar
			artifact testJar
		}
	}

	repositories {
		maven {
			url = repositorydir
		}
	}
}
/* End of Maven logic. */

compileJava.dependsOn clean